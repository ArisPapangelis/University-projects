.include "m16def.inc"

.def temp=r16
.def savedcode=r25
.def temp1=r24
.def temp0=r23
.def cutoff=r22
.def seconds=r21

.cseg
.org  0x0000	rjmp RESET

.org INT_VECTORS_SIZE	;Start storing the program after the interrupt handlers.


RESET:
	rcall DEBOUNCE	;Needed if resetting from the end of the program.

	ser temp
	out DDRB,temp	;Port B as output, port D as input.
	clr temp
	out DDRD,temp
	
	
	ldi temp,LOW(RAMEND)
	out SPL,temp
	ldi temp,HIGH(RAMEND)	;Initialize stack pointer, at the end of RAM.
	out SPH,temp
	

	ldi temp,(1<<CS12 | 1<<CS10)	;Prescaler = 1024
	out TCCR1B,temp					;Timer, at 4MHZ, 10s = 39060,5

	
	ldi savedcode,0xFF				;Modified when pressing buttons.
	ldi temp1,0b1111_1101			;For blinking led 1.
	ldi temp0,0b1111_1110			;For blinking led 0.
	ldi cutoff,0b1011_1101			;For when the inflow of water is cut off. (switch 7)
	
	

START:

	in temp,PIND		;Code input.
	and savedcode,temp

		
	in XL,TCNT1L	;We read the values in this order, so as not to
	in XH,TCNT1H	;potentially lose information.
	

	cpi XH,HIGH(39061)	
	brne START			;Check timer1 HIGH byte, in case 10s have passed.


	cpi XL,LOW(39061)	;Check timer1 LOW byte, in case 10s have passed.
	brlo START

	rcall DEBOUNCE

WAIT_SW6:
	sbic PIND,6		;Wait until switch 6 is pressed to start washing.
	rjmp WAIT_SW6

	rcall DEBOUNCE

	ldi temp,0		;Reset the timer for the next 10 seconds.
	out TCNT1H,temp
	out TCNT1L,temp
	

WAIT_SW1:
	sbis PIND,1		;In the next 10 seconds, check if the washing machine is overloaded.
	rjmp OVERLOAD

	in XL,TCNT1L	;We read the values in this order, so as not to
	in XH,TCNT1H	;potentially lose information.
	

	cpi XH,HIGH(39061)	
	brne WAIT_SW1			;Check timer1 HIGH byte, in case 10s have passed.


	cpi XL,LOW(39061)	;Check timer1 LOW byte, in case 10s have passed.
	brlo WAIT_SW1

	rjmp MAIN

OVERLOAD:			
	rcall DEBOUNCE

    ldi  r18, 13	;Originally 21, changed due to the last loop's added instructions(5 cycles in last).
    ldi  r19, 75
    ldi  r20, 191		; Generated by delay loop calculator	
L1: dec  r20			; at http://www.bretmulvey.com/avrdelay.html
    brne L1				; Delay 4 000 000 cycles
    dec  r19			; 1s at 4.0 MHz 
    brne L1

   	sbis PIND,1		;In case of overload, wait until switch 1 is pressed again.
	rjmp MAIN

    dec  r18
    brne L1
    
	
	out PORTB,temp1	
	com temp1				;Blink led 1 every 1 second.
	ori temp1,0b1111_1101

	rjmp OVERLOAD



MAIN:
	rcall DEBOUNCE

	sbrs savedcode,2	;If switch 2 has been pressed, do a prewash.
	rcall PREWASH
	ori savedcode,0b1100_0111	;After checking for a prewash, keep only the bits relevant for the main wash.
	
	ldi seconds,4
	cpi savedcode,0b1100_0111	
	breq WASH					;4 seconds washing program.
	cpi savedcode,0b1110_0111
	breq WASH

	
	ldi seconds,8
	cpi savedcode,0b1100_1111
	breq WASH					;8 seconds washing program.
	cpi savedcode,0b1110_1111
	breq WASH

	ldi seconds,12
	cpi savedcode,0b1101_0111
	breq WASH					;12 seconds washing program.
	cpi savedcode,0b1111_0111
	breq WASH


	ldi seconds,18
	cpi savedcode,0b1101_1111
	breq WASH					;18 seconds washing program.
	cpi savedcode,0b1111_1111
	breq WASH

	rjmp END		;Jump to the end of the program if no program was selected.





WASH:

	ldi temp,0b1111_0111	;Turn led 3 on for main wash.
	out PORTB,temp


; Generated by delay loop calculator
; at http://www.bretmulvey.com/avrdelay.html
;
; Delay 4 000 000 cycles
; 1s at 4.0 MHz

    ldi  r18, 9		;Originally 21, changed due to the added instructions in the last loop.
    ldi  r19, 75
    ldi  r20, 191
L3: dec  r20
    brne L3
    dec  r19
    brne L3

	sbis PIND,0
	rcall OPEN_DOOR		;Check if the door has been opened. (switch 0)
	sbis PIND,7
	rcall WATER_CUTOFF	;Check if the water inflow has been cut off. (switch 7)

	dec  r18
    brne L3
    lpm

	dec seconds		;The delay loop above will be run as many times as specified by the corresponding washing program.
	brne WASH


    
	sbrs savedcode,5	;Check if the selected washing program includes rinsing and drying. (switch 5)
	rcall RINSE
	
	rjmp END



PREWASH:
	ldi temp,0b1111_1011	;Turn led 2 on for prewash.
	out PORTB,temp

; Generated by delay loop calculator
; at http://www.bretmulvey.com/avrdelay.html
;
; Delay 16 000 000 cycles
; 4s at 4.0 MHz

    ldi  r18, 82
    ldi  r19, 43
    ldi  r20, 0
L2: dec  r20
    brne L2
    dec  r19
    brne L2
    dec  r18
    brne L2
    lpm

	ret

RINSE:
	ldi temp,0b1110_1111	;Turn led 4 on for rinsing.
	out PORTB,temp
	
; Generated by delay loop calculator
; at http://www.bretmulvey.com/avrdelay.html
;
; Delay 4 000 000 cycles
; 1s at 4.0 MHz

    ldi  r18, 21
    ldi  r19, 75
    ldi  r20, 191
L4: dec  r20
    brne L4
    dec  r19
    brne L4
    dec  r18
    brne L4
    nop
	
	
	rcall DRY		;After rinsing, call the drying subroutine.
	ret
	
DRY:
	ldi temp,0b1101_1111	;Turn led 5 on for drying.
	out PORTB,temp

; Generated by delay loop calculator
; at http://www.bretmulvey.com/avrdelay.html
;
; Delay 8 000 000 cycles
; 2s at 4.0 MHz

    ldi  r18, 41
    ldi  r19, 150
    ldi  r20, 128
L5: dec  r20
    brne L5
    dec  r19
    brne L5
    dec  r18
    brne L5

	ret

OPEN_DOOR:
	rcall DEBOUNCE
	
	ldi  r18, 13		;Originally 21, changed due to the last loop's added instructions(5 cycles in last).
    ldi  r19, 75
    ldi  r20, 191		; Generated by delay loop calculator	
L6: dec  r20			; at http://www.bretmulvey.com/avrdelay.html
    brne L6				; Delay 4 000 000 cycles
    dec  r19			; 1s at 4.0 MHz 
    brne L6

   	sbis PIND,0
	rjmp CLOSED_DOOR	;Wait until switch 0 is pressed again. (for the door to close)

    dec  r18
    brne L6
    
	
	out PORTB,temp0
	com temp1			;Blink led 0 every 1 second.
	ori temp1,0b1111_1110

	rjmp OPEN_DOOR

CLOSED_DOOR:
	ldi temp,0b1111_0111	;If the door has been closed, return to main wash, and turn the appropriate leds on again.
	out PORTB,temp
	rcall DEBOUNCE
	ret



WATER_CUTOFF:
	rcall DEBOUNCE

	ldi  r18, 13		;Originally 21, changed due to the last loop's added instructions(5 cycles in last).
    ldi  r19, 75
    ldi  r20, 191		; Generated by delay loop calculator	
L7: dec  r20			; at http://www.bretmulvey.com/avrdelay.html
    brne L7				; Delay 4 000 000 cycles
    dec  r19			; 1s at 4.0 MHz 
    brne L7

   	sbis PIND,7
	rjmp NO_CUTOFF		;Wait until switch 7 is pressed again. (for the water inflow to be restored)

    dec  r18
    brne L7
    
	
	out PORTB,cutoff
	com cutoff
	ori cutoff,0b1111_1101	;Blink led 1 every 1 second.
	andi cutoff,0b1011_1111	;Turn led 6 on.

	rjmp WATER_CUTOFF

NO_CUTOFF:
	ldi temp,0b1111_0111	;If the inflow of water has been restored, return to main wash, and turn the appropriate leds on again.
	out PORTB,temp
	rcall DEBOUNCE
	ret


DEBOUNCE:
	in temp,PIND			;Debounce routine for the switches. Called before every input, in order not to get previous inputs due to the execution 
	cpi temp,0b1111_1111	;speed of the program.
	brne DEBOUNCE
	ret

END:
	ldi temp,0b0111_1111	;Turn led 7 on for 5 seconds, after the end of main wash.
	out PORTB,temp

; Generated by delay loop calculator
; at http://www.bretmulvey.com/avrdelay.html
;
; Delay 20 000 000 cycles
; 5s at 4.0 MHz

    ldi  r18, 102
    ldi  r19, 118
    ldi  r20, 194
L8: dec  r20
    brne L8
    dec  r19
    brne L8
    dec  r18
    brne L8

	ldi temp,0b1111_1111 ;Turn led 7 off after 5 seconds have passed.
	out PORTB,temp

ENDLOOP:
	sbis PIND,0		;When switch 0 is pressed, reset the washing machine. (jump to the start of the program)
	rjmp RESET
	rjmp ENDLOOP

